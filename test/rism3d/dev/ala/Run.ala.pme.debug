#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP Tests PME potential gradients vs. analytic forces
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

# Since the unit cell is much larger than the solute, the force error
# should be close to non-periodic.

#TODO: NOT YET OPTIMIZED. Attempts to balance accuracy and speed.

if( ! $?TESTsander ) set TESTsander = "${AMBERHOME}/bin/sander"

if ! { ../../checkrismunsupported.sh $TESTsander } exit 0

if( ! $?DO_PARALLEL ) then
        setenv DO_PARALLEL " "
else
        set numprocs=`echo $DO_PARALLEL | awk -f ../../numprocs.awk `
        if ( $numprocs > 2 ) then
            echo " DO_PARALLEL set to $DO_PARALLEL"
            goto toomany
        else if ( $?MP_PROCS)then
            if ($MP_PROCS > 3)then
#               --- For IBM AIX ---
                echo "    MP_PROCS set to "$MP_PROCS
                goto toomany
            endif
        endif
endif

cat > mdin <<EOF
Alanine dipeptide run
 &cntrl
    ntx=1, ntpr=2, ntwx=0, ntwr=0
    ioutfm=1
    imin=1, drms=1E-4
    maxcyc=1, ntmin=3
    ig=314159
    ntb=1
    irism=1
    cut=8.0
 /
 &rism
    periodic='pme'
    closure='kh'
    buffer=15, grdspc=0.5,0.5,0.5
    solvcut=12.d0
    verbose=1
    npropagate=5
    mdiis_del=0.7, mdiis_nvec=5, tolerance=1e-10
    apply_rism_force=1
 /
 &debugf
  ! Enable debugging.
  do_debugf=1
  ! Atom spatial offset. Negative log of delta used in numerical
  ! differentiating; e.g. 4 means delta is 10^-4 Angstroms.
  neglgdel=5
  ! Number of random atoms to test.
  nranatm=5
  ! Check atomic and molecular virials.
  chkvir=0
  ! Dump energies, forces, and virials to forcedump.dat.
  dumpfrc=0
  ! Compare energies, forces and virials to forcedump.dat.
  rmsfrc=0
  ! Zero out charge or van der Waals parameters.
  zerochg=0
  zerovdw=0
  ! Real-space (direct) non-bonded interactions (van der Waals, electrostatic).
  do_dir=0
  ! k-space (reciprocal) portion of Ewald sum.
  do_rec=0
  ! Adjust Ewald sum to remove the Gaussian charge contribution from
  ! neighboring bonded atoms and other atoms that should not have
  ! non-bonded pair forces.
  do_adj=0
  ! Remove from Ewald sum the interaction of point charge with its own
  ! Gaussian.
  do_self=0
  ! Bond forces.
  do_bond=0
  ! Angle forces.
  do_angle=0
  ! Dihedral forces.
  do_ephi=0
  ! cap force (whatever that is).
  do_cap=0
  ! Apply harmonic position constraints.
  doxconst=0
  ! 1-4 interactions (???).
  do_14=0
  ! Targeted MD simulation with restraints based on RMSD.
  do_tgt=0
  ! PBSA related routines.
  do_pbdir=0
  do_pbnp=0
  do_pbfd=0
 /
EOF

$DO_PARALLEL $TESTsander -O -i mdin -o ala.pme.debug.out \
    -p ../../ala/ala.parm7 -c ala.min.ortho.nc \
    -xvv ../../../../AmberTools/test/rism1d/spc-kh/spc.xvv.save \
    || goto error

# ../../../dacdif -a 1.e-6 ala.pme.debug.out.save ala.pme.debug.out
/bin/rm -f mdin mdinfo restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

toomany:
    echo " too many processors for this test, exiting"
    echo "============================================================"
    exit(0)








